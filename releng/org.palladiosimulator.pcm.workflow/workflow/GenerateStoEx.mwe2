module org.palladiosimulator.commons.GenerateStoex

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

import org.eclipse.emf.mwe.utils.StandaloneSetup
import tools.mdsd.ecoreworkflow.mwe2lib.xtext.config.TychoStandardLayoutProjectConfig

var projectName = "org.palladiosimulator.pcm.stoex"
var workspaceRoot = "../.."


Workflow {

    bean = StandaloneSetup {
        scanClassPath = true
        platformUri = "${workspaceRoot}"
        uriMap = {
            from = "platform:/plugin/org.eclipse.uml2.codegen.ecore/model/GenModel.ecore"
            to = "platform:/resource/org.eclipse.uml2.codegen.ecore/model/GenModel.ecore"
        }
        uriMap = {
            from = "platform:/plugin/de.uka.ipd.sdq.stoex/model/stoex.ecore"
            to = "platform:/resource/de.uka.ipd.sdq.stoex/model/stoex.ecore"
        }
        uriMap = {
            from = "platform:/plugin/de.uka.ipd.sdq.probfunction/model/ProbabilityFunction.ecore"
            to = "platform:/resource/de.uka.ipd.sdq.probfunction/model/ProbabilityFunction.ecore"
        }
        uriMap = {
            from = "platform:/plugin/de.uka.ipd.sdq.units/model/Units.ecore"
            to = "platform:/resource/de.uka.ipd.sdq.units/model/Units.ecore"
        }
        uriMap = {
            from = "platform:/plugin/org.palladiosimulator.pcm/model/pcm.ecore"
            to = "platform:/resource/org.palladiosimulator.pcm/model/pcm.ecore"
        }
        registerGeneratedEPackage = "de.uka.ipd.sdq.stoex.StoexPackage"
        registerGeneratedEPackage = "de.uka.ipd.sdq.probfunction.ProbfunctionPackage"
        registerGeneratedEPackage = "de.uka.ipd.sdq.units.UnitsPackage"
        registerGeneratedEPackage = "org.eclipse.uml2.codegen.ecore.genmodel.GenModelPackage"
        registerGenModelFile = "platform:/resource/de.uka.ipd.sdq.stoex/model/stoex.genmodel"
        registerGenModelFile = "platform:/resource/de.uka.ipd.sdq.probfunction/model/ProbabilityFunction.genmodel"
        registerGenModelFile = "platform:/resource/de.uka.ipd.sdq.units/model/Units.genmodel"
        registerGenModelFile = "platform:/resource/org.eclipse.uml2.codegen.ecore/model/GenModel.genmodel"
    }

    component = XtextGenerator {
        configuration = {
            project = TychoStandardLayoutProjectConfig {
                baseName = "${projectName}"
                rootPath = workspaceRoot
                runtime = {
                    name = "org.palladiosimulator.pcm"
                    srcGen = "${workspaceRoot}/bundles/org.palladiosimulator.pcm/xtext-gen"
                }
                runtimeTest = {
                    enabled = true
                }
                eclipsePlugin = {
                    enabled = true
                }
                eclipsePluginTest = {
                    enabled = true
                }
                createEclipseMetaData = true
            }
            code = {
                encoding = "UTF-8"
                lineDelimiter = "\n"
                fileHeader = "/*\n * generated by Xtext \${version}\n */"
                preferXtendStubs = false
            }
        }
        
        language = StandardLanguage {
            name = "${projectName}.PCMStoex"
            fileExtensions = "pcmstoex"
			referencedResource = "platform:/resource/de.uka.ipd.sdq.stoex/model/stoex.genmodel"
			referencedResource = "platform:/resource/org.palladiosimulator.pcm/model/pcm.genmodel"
			
			fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}

			formatter = {
				generateStub = true
				generateXtendStub = true
			}

			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				// Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
				generateDeprecationValidation = true
			}
			junitSupport = {
				junitVersion = "5"
			}
		}
	}
}
